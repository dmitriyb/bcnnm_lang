define immovable cell FSMC from AnimalCell #define frog sartorius muscle cell
#based on parameters of frog sartorius muscle fibers (Keener, Sneyd _ 2009)

    constants:
        #molecules
        Na_o:   110 *  (10 ^ (-6))   #the extracellular sodium concentration (M cm^(-3))
        K_o:    2.5 *  (10 ^ (-6))   #the extracellular potassium concentration (M cm^(-3))
        Cl_o:   90 *  (10 ^ (-6))    #the extracellular chloride concentration (M cm^(-3))
        x:      2.6 * (10 ^ (-14))   #number of moles of the impermeant intracellular molecules (M)


        #other
        r:    2.5 * (10 ^ (-3))      #cell radius (cm)
        g_Cl: 0.2 * (10 ^ (-3))      #conductance of Cl ion (S cm^(-2))
        g_K:  0.3 * (10 ^ (-3))      #conductance of K ion (S cm^(-2))
        g_Na: 0.01 * (10 ^ (-3))     #conductance of Na ion (S cm^(-2))
        F:    9.648533 * (10 ^ 4)    #Faraday's constant (С mol^(-1))
        n:    3                      #number of Na ions pumped per Na/K ATPase cycle
        q:    2                      #number of K ions pumped per Na/K ATPase cycle
        p:    25                     #Na/K ATPase pump rate (C сm^(-2) s^(-1))
        P_f:  7 * (10 ^ (-3))        #osmotic water permeability coefficient (cm s^(-1))
        A:    7.854 * (10 ^ (-5))    #membrane area (cm^2) / counted geometrically from cell radius
        R:    8.3144598 * (10 ^ 7)   #universal gas constant (cm^2 g s^(-2) K^(-1) M^(-1))
        T:    298.15                 #absolute temperature (K)
        z:    -1                     #average charge of impermeant intracellular molecules
        v_w:  18                     #partial molar volume of water (cm^3 mol^(-1))
        V:    -99 * (10 ^ (-3))      #resting membrane potential (Volt)

    variables:
        #molecules
        Na:   13 *  (10 ^ (-6))      #the intracellular sodium concentration (M cm^(-3))
        K:    138 *  (10 ^ (-6))     #the intracellular potassium concentration (M cm^(-3))
        Cl:   3 *  (10 ^ (-6))       #the intracellular chloride concentration (M cm^(-3))

        #other
        Os_o: 238 * (10 ^ -6)       #the extracellular osmolarity (Osmol cm^(-3)) / isotonic amphibian Ringer solution (muscle)
        Os:   238 * (10 ^ -6)       #the intracellular osmolarity
        w :   6.554 * (10 ^ (-8))   #cell volume / counted geometrically from cell radius

    compartments:
        none


    conditions:
        none

    mechanisms:
        declaration:
            cyclic IntracellularSodiumChange(duration: 1000000) from Dynamic       #intracellular sodium concentration change
                parameters: (Na)
                delta: (- A / w / F) * (g_Na * (V - (R * T / F) * ln(Na_o / Na)) + n * p) - (1 / w * v_w * P_f * A * (Os - Os_o)) * Na

                #where (v_w * P_f * A * (Os - Os_o)) is the delta(w)

            cyclic IntracellularPotassiumChange(duration: 1000000) from Dynamic    #intracellular potassium concentration change
                parameters: (K)
                delta: (- A / w / F) * (g_K * (V - (R * T / F) * ln(K_o / K)) - q * p) - (1 / w * v_w * P_f * A * (Os - Os_o)) * K

            cyclic IntracellularChlorideChange(duration: 1000000) from Dynamic     #intracellular chloride concentration change
                parameters: (Cl)
                delta: (A / w / F) * (g_Cl * (V - (R * T / F) * ln(Cl_o / Cl))) - (1 / w * v_w * P_f * A * (Os - Os_o)) * Cl

            cyclic IntracellularOsmolarityChange(duration: 1000000) from Dynamic     #intracellular osmolarity change
                parameters: (Os)
                delta: ((- A / w / F) * (g_Na * (V - (R * T / F) * ln(Na_o / Na)) + n * p) - (1 / w * v_w * P_f * A * (Os - Os_o)) * Na) + ((- A / w / F) * (g_K * (V - (R * T / F) * ln(K_o / K)) - q * p) - (1 / w * v_w * P_f * A * (Os - Os_o)) * K) - ((A / w / F) * (g_Cl * (V - (R * T / F) * ln(Cl_o / Cl))) - (1 / w * v_w * P_f * A * (Os - Os_o)) * Cl)

            cyclic CellVolume(duration: 1000000) from Dynamic                      #cell volume change
                parameters: (w)
                delta: v_w * P_f * A * (Os - Os_o)



        initial:
            IntracellularSodiumChange
            IntracellularPotassiumChange
            IntracellularChlorideChange
            IntracellularOsmolarityChange
            CellVolume

        mutually exclusive:
           none
